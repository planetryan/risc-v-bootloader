OUTPUT_ARCH( "riscv:rv64gc" )
ENTRY(_start)

MEMORY
{
  ram (rwx) : ORIGIN = 0x80000000, LENGTH = 128M
}

SECTIONS
{
    . = 0x80000000;

    .text : {
        *(.text.entry); /* Keep this first */
        *(.text*);
    } > ram

    .rodata : {
        *(.srodata*);
        *(.rodata*);
    } > ram

    .data : {
        *(.sdata*);
        *(.data*);
    } > ram

    .bss : {
        __bss_start = .;
        *(.sbss*);
        *(.bss*);
        __bss_end = .;
    } > ram

    /DISCARD/ : {
        *(.comment);
        *(.note*);
        *(.eh_frame*);
    }

    . = ALIGN(4096); /* align to page size */

    /* Stabs debugging sections.  */
    .stab 0 : { *(.stab) }
    .stabstr 0 : { *(.stabstr) }
    .stab.excl 0 : { *(.stab.excl) }
    .stab.exclstr 0 : { *(.stab.exclstr) }
    .stab.index 0 : { *(.stab.index) }
    .stab.indexstr 0 : { *(.stab.indexstr) }

    /* DWARF debugging sections.
       See the section "Linking with DWARF" in the GNU ld documentation.  */
    .debug_aranges 0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    .debug_info 0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev 0 : { *(.debug_abbrev) }
    .debug_line 0 : { *(.debug_line .gnu.linkonce.wl.*) }
    .debug_frame 0 : { *(.debug_frame) }
    .debug_str 0 : { *(.debug_str) }
    .debug_loc 0 : { *(.debug_loc) }
    .debug_ranges 0 : { *(.debug_ranges) }
}